# (c) Silvaco Inc., 2018
go atlas
title AlGaAs HBT simulation with DD model
#
# SECTION 1: Mesh spectification
#
mesh  space.mult=1.0
# 
x.mesh loc=0.00 spac=0.25
x.mesh loc=2.00 spac=0.25
x.mesh loc=3.00 spac=0.2
x.mesh loc=4.00 spac=0.2
#
y.mesh loc=0.00 spac=0.03
y.mesh loc=0.15 spac=0.005
y.mesh loc=0.20 spac=0.015
y.mesh loc=0.23 spac=0.01
y.mesh loc=0.63 spac=0.05
y.mesh loc=1.13 spac=0.03

#
# SECTION 2: Structure definition
#
region num=1 material=AlGaAs x.min=3 x.max=4  y.min=0.0 y.max=0.15 x.comp=0.3 
\ grad.34=0.15
region num=2 material=GaAs  x.min=0 x.max=4 y.min=0.15 y.max=0.23
region num=3 material=GaAs  x.min=0 x.max=4 y.min=0.23 y.max=0.63
region num=4 material=GaAs  x.min=0 x.max=4 y.min=0.63 y.max=1.13  


# Etching 
region num=5 mat=air x.max=3  y.max=0.15


elec num=1  name=emitter x.min=3.0 x.max=4.0 y.min=0.0 y.max=0.0
elec num=2  name=base    x.min=0.0 x.max=2.0 y.min=0.15 y.max=0.15
elec num=3  name=collector bot 
#
doping uniform region=1 n.type conc=2.e17
doping uniform region=2 p.type conc=2.e18
doping uniform region=3 n.type conc=5.e16
doping uniform region=4 n.type conc=2.e17

#
# SECTION 3: Set models and define material parameters
#
material taun0=1.e-9 taup0=1.e-9 
material material=AlGaAs mun=2200 mup=350 
material align=0.6

model  srh fldmob   print  optr  
model material=GaAs conmob evsatmod=1 

#
# SECTION 4: Initial solution and collector bias ramp
solve init

save outf=HBT.str
tonyplot  HBT.str -set HBT.set

method gummel  newton trap  vsatmod.inc=0.01  

solve
solve local vcollector=2.5

#
# SECTION 5: Calculations for Gummel plot
#

log outf=HBTplot.log master



solve vbase=0.1

method  newton  trap autonr vsatmod.inc=0.01 itlim=50

solve vbase=0.1 vstep=0.1 name=base vfinal=1.3
solve vbase=1.3 vstep=0.025 name=base vfinal=1.4


tonyplot HBTplot.log -set HPTplotcurve.set



quit
